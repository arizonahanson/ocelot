{
package parser

import "github.com/starlight/ocelot/pkg/core"
}

// root of AST
Module ←  seq:Seq EOF {
  return core.Expr(seq.([]core.Any)), nil
}

// expression sequence without delimiters
Seq ←  _* first:Any? rest:(_+ Any)* _* {
  return join(first, rest, 1), nil
}

// parent `any` type
Any ←   Atom / Symbol / Expr

// core ECMA-404 types (literals)
Atom ←  Number / String / Vector / Hash

// s-expression
Expr ←  '(' seq:Seq ')' {
  return core.Expr(seq.([]core.Any)), nil
} / '(' Seq !')' {
  return core.Null{}, errors.New("not terminated")
}

// vector (array)
Vector ←  '[' seq:Seq ']' {
  return core.Vector(seq.([]core.Any)), nil
} / '[' Seq !']' {
  return core.Null{}, errors.New("not terminated")
}

// hash-map (object)
Hash ←  '{' _* first:(String _* ':' _* Any)? rest:(_+ String _* ':' _* Any)* _* '}' {
  return core.Hash(merge(first, rest, 0, 4)), nil
} / '{' _* (String _* ':' _* Any) (_+ String _* ':' _* Any)* _* !'}' {
  return core.Null{}, errors.New("not terminated")
}

// real number (eg. -123.45e-67)
Number ←  '-'? digit+ ('.' digit+)? ('e'i ('+' / '-')? digit+)? {
  return core.String{Val: string(c.text)}.Number()
}

// quoted string
String ←  '"' runeChr* '"' {
  return core.String{Val: string(c.text)}.Unquote()
} / '"' runeChr* !'"' {
	return core.Null{}, errors.New("not terminated")
}
// no naked " or \ inside a String, supports \\, \/, \", \abfnrtv, \xff, \uffff, \Uffffffff
runeChr ←  [^"\\] / runeEsc
runeEsc ←  `\` (["\\/abfnrtv] /
           ('x' hexDigit hexDigit) /
           ('u' hexDigit hexDigit hexDigit hexDigit) /
           ('U' hexDigit hexDigit hexDigit hexDigit hexDigit hexDigit hexDigit hexDigit))
hexDigit ← [0-9a-f]i

// null, true, false and symbols (identifiers)
Symbol ←  word ('.' word)* suffix? {
  str := string(c.text)
  if str == "null" {
    return core.Null{}, nil
  }
  if str == "true" {
    return core.Bool(true), nil
  }
  if str == "false" {
    return core.Bool(false), nil
  }
  return core.NewSymbol(str, pos(c.pos)), nil
}
// symbol component
word ←  letter (letter / digit)*
// unicode "letters" for symbols
letter ←  [\p{L}] / '_'
// numerals
digit ←  [0-9]
// symbol suffix
suffix ←  [!?*]

// UTF-8: whitespace and control chars/unused codes
_ "whitespace" ←  [\p{Z}] / [\p{C}] / ',' / Comment

// comments
Comment ←  SingleLineComment / MultiLineComment
SingleLineComment ← "//" (!EOL .)* EOL
MultiLineComment ←  "/*" (!"*/" .)* "*/"

// end of line
EOL ←  '\n' / EOF
// End of file
EOF ←  !.
